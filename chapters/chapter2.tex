\documentclass[../main.tex]{subfiles}

\begin{document}

% exercise 2.1

\section{}

Code:

\begin{lstlisting}
(define (make-rat n d)
  (if (< d 0)
      (make-rat (- 0 n) (- 0 d))
      (let ((g (gcd n d)))
        (cons (/ n g) (/ d g)))))
\end{lstlisting}

% exercise 2.2

\section{}

Code:

\begin{lstlisting}
;; point definition
(define (make-point x y)
  (cons x y))
(define (x-point point)
  (car point))
(define (y-point point)
  (cdr point))

;; segment definition
(define (make-segment start end)
  (cons start end))
(define (start-segment segment)
  (car segment))
(define (end-segment segment)
  (cdr segment))
(define (midpoint-segment segment)
  (define (average x y)
    (/ (+ x y) 2.0))
  (let ((start (start-segment segment))
        (end (end-segment segment)))
    (make-point (average (x-point start)
                         (x-point end))
                (average (y-point start)
                         (y-point end)))))
\end{lstlisting}

% exercise 2.3

\section{}

Code:

\begin{lstlisting}
;; one version of rectangle
(define (make-rect top-left bottom-right)
  (cons top-left bottom-right))
(define (top-left rect)
  (car rect))
(define (bottom-right rect)
  (cdr rect))
(define (width rect)
  (- (x-point (bottom-right rect))
     (x-point (top-left rect))))
(define (height rect)
  (- (y-point (bottom-right rect))
     (y-point (top-left rect))))

;; another version of rectangle
(define (make-rect top-left width height)
  (cons top-left (cons width height)))
(define (top-left rect)
  (car rect))
(define (width rect)
  (car (cdr rect)))
(define (height rect)
  (cdr (cdr rect)))

;; rect procedures
(define (perimeter rect)
  (* 2 (+ (width rect) (height rect))))
(define (area rect)
  (* (width rect) (height rect)))
\end{lstlisting}

% exercise 2.4

\section{}

By evaluating \lstinline{(car (cons x y))} using
 applicative-order strategy, it can be verified
 that \lstinline{(car (cons x y))} yields
 \lstinline{x} for any object \lstinline{x} and
 \lstinline{y}:

\begin{lstlisting}
(car (cons x y)) ->
(car (lambda (m) (m x y))) ->
((lambda (m) (m x y)) (lambda (p q) p)) ->
((lambda (p q) p) x y) ->
x
\end{lstlisting}

The same result can also be obtained using
 normal-order strategy:

\begin{lstlisting}
(car (cons x y)) ->
((cons x y) (lambda (p q) p)) ->
((lambda (m) (m x y)) (lambda (p q) p)) ->
((lambda (p q) p) x y) ->
x
\end{lstlisting}

The corresponding definition of \lstinline{cdr}:

\begin{lstlisting}
(define (cdr z)
  (z (lambda (p q) q)))
\end{lstlisting}

% exercise 2.5

\section{}

Code:

\begin{lstlisting}
(define (cons x y)
  (* (expt 2 x) (expt 3 y)))
(define (car z)
  (define (car-iter z sum)
    (if (= (remainder z 2) 0)
        (car-iter (/ z 2) (+ 1 sum))
        sum))
  (car-iter z 0))
(define (cdr z)
  (define (cdr-iter z sum)
    (if (= (remainder z 3) 0)
        (cdr-iter (/ z 3) (+ 1 sum))
        sum))
  (cdr-iter z 0))
\end{lstlisting}

% exercise 2.6

\section{}

Code:

\begin{lstlisting}
(define one (lambda (f) (lambda (x) (f x))))
(define two (lambda (f) (lambda (x) (f (f x)))))
(define (add a b)
  (lambda (f) (lambda (x) ((a f) ((b f) x)))))
\end{lstlisting}

\end{document}










